# Copyright 1999-2026 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=8

# calibre 9.x sets requires-python = ">=3.14" in pyproject.toml and setup.py
# aborts on anything older, so this cannot be widened. python3_15 is left out
# on purpose: dev-lang/python:3.15 is still at _rc in ::gentoo and upstream
# does not test against it.
PYTHON_COMPAT=( python3_14 )
PYTHON_REQ_USE="sqlite,ssl"

inherit edo toolchain-funcs python-single-r1 qmake-utils xdg

DESCRIPTION="Ebook management application"
HOMEPAGE="https://calibre-ebook.com/"
SRC_URI="https://github.com/kovidgoyal/calibre/releases/download/v${PV}/${P}.tar.xz -> ${P}.tar.xz"

LICENSE="
	GPL-3+
	GPL-3
	GPL-2+
	GPL-2
	GPL-1+
	LGPL-3+
	LGPL-2.1+
	LGPL-2.1
	BSL
	MIT
	Old-MIT
	Apache-2.0
	public-domain
	|| ( Artistic GPL-1+ )
	CC-BY-3.0
	OFL-1.1
	PSF-2
"
SLOT="0"
KEYWORDS="~amd64"
IUSE="+font-subsetting ios speech +system-mathjax test +udisks unrar"

RESTRICT="!test? ( test )"

REQUIRED_USE="${PYTHON_REQUIRED_USE}"

COMMON_DEPEND="${PYTHON_DEPS}
	app-i18n/uchardet
	>=app-text/hunspell-1.7:=
	>=app-text/podofo-1.1.0:1=[jpeg,png]
	app-text/poppler[utils]
	dev-libs/hyphen:=
	>=dev-libs/icu-57.1:=
	dev-libs/openssl:=
	dev-libs/snowball-stemmer:=
	$(python_gen_cond_dep '
		>=dev-python/apsw-3.25.2_p1[${PYTHON_USEDEP}]
		dev-python/beautifulsoup4[${PYTHON_USEDEP}]
		>=dev-python/css-parser-1.0.4[${PYTHON_USEDEP}]
		dev-python/dnspython[${PYTHON_USEDEP}]
		>=dev-python/feedparser-6.0.14[${PYTHON_USEDEP}]
		>=dev-python/html2text-2019.8.11[${PYTHON_USEDEP}]
		>=dev-python/html5-parser-0.4.9[${PYTHON_USEDEP}]
		dev-python/jeepney[${PYTHON_USEDEP}]
		>=dev-python/lxml-3.8.0[${PYTHON_USEDEP}]
		dev-python/lxml-html-clean[${PYTHON_USEDEP}]
		>=dev-python/markdown-3.0.1[${PYTHON_USEDEP}]
		>=dev-python/mechanize-0.3.5[${PYTHON_USEDEP}]
		>=dev-python/msgpack-0.6.2[${PYTHON_USEDEP}]
		>=dev-python/netifaces-0.10.5[${PYTHON_USEDEP}]
		>=dev-python/pillow-3.2.0[jpeg,truetype,webp,zlib,${PYTHON_USEDEP}]
		>=dev-python/psutil-4.3.0[${PYTHON_USEDEP}]
		>=dev-python/pychm-0.8.6[${PYTHON_USEDEP}]
		dev-python/pykakasi[${PYTHON_USEDEP}]
		>=dev-python/pygments-2.3.1[${PYTHON_USEDEP}]
		>=dev-python/python-dateutil-2.5.3[${PYTHON_USEDEP}]
		dev-python/pyqt6[gui,network,opengl,printsupport,quick,svg,widgets,${PYTHON_USEDEP}]
		dev-python/pyqt6-webengine[widgets,${PYTHON_USEDEP}]
		dev-python/pystache[${PYTHON_USEDEP}]
		dev-python/regex[${PYTHON_USEDEP}]
		dev-python/tzlocal[${PYTHON_USEDEP}]
		dev-python/xxhash[${PYTHON_USEDEP}]
		>=dev-python/zeroconf-0.75.0[${PYTHON_USEDEP}]
	')
	dev-qt/qtbase:6=[gui,widgets]
	dev-qt/qtimageformats:6
	system-mathjax? ( >=dev-libs/mathjax-3:= )
	media-fonts/liberation-fonts
	media-libs/fontconfig:=
	>=media-libs/freetype-2:=
	>=media-libs/libmtp-1.1.11:=
	>=media-gfx/optipng-0.7.6
	>=media-video/ffmpeg-6:=
	virtual/libusb:1=
	x11-misc/shared-mime-info
	>=x11-misc/xdg-utils-1.0.2-r2
"
RDEPEND="${COMMON_DEPEND}
	font-subsetting? ( $(python_gen_cond_dep 'dev-python/fonttools[${PYTHON_USEDEP}]') )
	ios? (
		>=app-pda/usbmuxd-1.0.8
		>=app-pda/libimobiledevice-1.2.0
	)
	speech? (
		$(python_gen_cond_dep 'app-accessibility/speech-dispatcher[python,${PYTHON_USEDEP}]')
		dev-python/pyqt6[multimedia,speech]
	)
	system-mathjax? ( >=dev-libs/mathjax-3:= )
	udisks? ( virtual/libudev sys-fs/udisks:2 )
	unrar? ( dev-python/unrardll )
"
DEPEND="${COMMON_DEPEND}
	test? ( $(python_gen_cond_dep '>=dev-python/chardet-3.0.3[${PYTHON_USEDEP}]') )
"
BDEPEND="$(python_gen_cond_dep '
		>=dev-python/pyqt-builder-1.10.3[${PYTHON_USEDEP}]
		>=dev-python/sip-5[${PYTHON_USEDEP}]
	')
	virtual/pkgconfig
	app-misc/pax-utils
	dev-build/cmake
	dev-util/desktop-file-utils
	dev-util/gtk-update-icon-cache
	system-mathjax? ( >=dev-lang/rapydscript-ng-0.8.5 )
"

PATCHES=(
	"${FILESDIR}/${PN}-9.15.0-jxr-test.patch"
	"${FILESDIR}/${PN}-9.15.0-piper.patch"
)

src_prepare() {
	default

	# Disable privilege dropping for bug #287067 and generally because desktop
	# login user != portage.
	sed -e "s:SUDO_:__DISABLED_SUDO_:" \
		-i setup/__init__.py || die

	# Delete the rapydscript-ng compiler embedded in qtwebengine. It violates
	# the portage sandbox (tries to mkdir inside /usr) and is unnecessary.
	# Preserve other files in resources/ (e.g. scripts.calibre_msgpack).
	rm -rf resources/rapydscript || die
}

src_compile() {
	# TODO: get qmake called by setup.py to respect CC and CXX too
	tc-export CC CXX

	# bug 821871
	local MY_LIBDIR="${ESYSROOT}/usr/$(get_libdir)"
	export FT_LIB_DIR="${MY_LIBDIR}" HUNSPELL_LIB_DIR="${MY_LIBDIR}"

	# app-text/podofo:1 is installed into a private prefix so that it can
	# coexist with slot 0.
	local podofo_libdir="${ESYSROOT}/usr/$(get_libdir)/podofo-1"
	export PODOFO_INC_DIR="${ESYSROOT}/usr/include/podofo-1/podofo"
	export PODOFO_LIB_DIR="${podofo_libdir}"
	export PODOFO_LIB_NAME="${podofo_libdir}/libpodofo.so"
	export LDFLAGS="${LDFLAGS} -Wl,-rpath,${EPREFIX}/usr/$(get_libdir)/podofo-1"
	export QMAKE="$(qt6_get_bindir)/qmake"

	edo ${EPYTHON} setup.py build

	# Guard: assert the extension needs the SONAME of the slot it was compiled
	# against.
	local want got
	want=$(scanelf -qF '%S#F' "${podofo_libdir}/libpodofo.so") || die
	got=$(scanelf -qF '%n#F' src/calibre/plugins/podofo.so) || die
	[[ ,${got}, == *,${want},* ]] ||
		die "podofo.so needs '${got}', expected '${want}': the wrong PoDoFo slot was linked in"
	edo ${EPYTHON} setup.py gui

	# Use system liberation fonts instead of vendored ones.
	# The release tarball already contains pre-built resources (mathjax,
	# hyphenation, piper_voices, etc.), so we only need to replace fonts.
	edo ${EPYTHON} setup.py liberation_fonts \
		--path-to-liberation_fonts "${EPREFIX}"/usr/share/fonts/liberation-fonts \
		--system-liberation_fonts
	if use system-mathjax; then
		edo ${EPYTHON} setup.py mathjax --path-to-mathjax "${EPREFIX}"/usr/share/mathjax --system-mathjax
		edo ${EPYTHON} setup.py rapydscript
	fi
}

src_test() {
	# Skipped tests:
	local _test_excludes=(
		# unpackaged Python dependency: py7zr
		7z
		# unpackaged Python dependency: pyzstd
		test_zstd
		# unpackaged TTS backend (optional at runtime)
		piper
		# tests if a completely unused module is bundled
		pycryptodome

		$(usev !speech speech_dispatcher)
		$(usev !unrar test_unrar)

		# undocumented reasons
		test_mem_leaks
		test_searching
	)

	use speech || export SKIP_SPEECH_TESTS=1

	edo ${PYTHON} setup.py test "${_test_excludes[@]/#/--exclude-test-name=}"
}

src_install() {
	# Bug #352625 - Some LANGUAGE values can trigger a ValueError
	export -n LANG LANGUAGE ${!LC_*}
	export LC_ALL=C.UTF-8 # bug #709682

	# Bug #295672 - Avoid sandbox violation in ~/.config
	export HOME="${T}/fake_homedir"
	export CALIBRE_CONFIG_DIRECTORY="${HOME}/.config/calibre"
	mkdir -p "${CALIBRE_CONFIG_DIRECTORY}" || die

	addpredict /dev/dri #665310

	# If this directory doesn't exist, zsh completion won't install
	dodir /usr/share/zsh/site-functions

	edo "${PYTHON}" setup.py install \
		--staging-root="${ED}/usr" \
		--prefix="${EPREFIX}/usr" \
		--libdir="${EPREFIX}/usr/$(get_libdir)" \
		--staging-libdir="${ED}/usr/$(get_libdir)" \
		--system-plugins-location="${EPREFIX}/usr/share/calibre/system-plugins"

	cp -r man-pages/ "${ED}"/usr/share/man || die

	find "${ED}"/usr/share -type d -empty -delete || die

	python_fix_shebang "${ED}/usr/bin"

	python_optimize "${ED}"/usr/$(get_libdir)/calibre "${D}/$(python_get_sitedir)"

	newinitd "${FILESDIR}"/calibre-server-3.init calibre-server
	newconfd "${FILESDIR}"/calibre-server-3.conf calibre-server
}
